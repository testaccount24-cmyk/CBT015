*          DATA SET CLIPV3     AT LEVEL 003 AS OF 09/20/85
* MODIFY TO USE IOSVSUCB ROUTINE                                        00001
*          DATA SET CLIP       AT LEVEL 004 AS OF 01/26/83              00002
CLIP     TITLE 'CLIP - RELABEL A DISK PACK'                             00003
R0       EQU   0                                                        00004
R1       EQU   1                                                        00005
R2       EQU   2                                                        00006
R3       EQU   3                                                        00007
R4       EQU   4                                                        00008
R5       EQU   5                                                        00009
R6       EQU   6                                                        00010
R7       EQU   7                                                        00011
R8       EQU   8                                                        00012
R9       EQU   9                                                        00013
R10      EQU   10                                                       00014
R11      EQU   11                                                       00015
R12      EQU   12                                                       00016
R13      EQU   13                                                       00017
R14      EQU   14                                                       00018
R15      EQU   15                                                       00019
*********************************************************************** 00020
*                                                                     * 00021
*        THIS PROGRAM WILL RELABEL ANY OFFLINE DISK PACK. THE PROGRAM * 00022
*        IS USED AS A STARTED TASK AND INTERACTION IS DONE THROUGH    * 00023
*        WTO AND WTOR. ANY NUMBER OF PACKS CAN BE RELABELED OR        * 00024
*        INSPECTED IN ONE EXECUTION OF THIS PROGRAM.                  * 00025
*                                                                     * 00026
*                                                                     * 00027
*        LINKEDIT ATTRIBUTES: AC=1                                    * 00028
*                                                                     * 00029
*        JCL REQUIRED:                                                * 00030
*                                                                     * 00031
*        //CLIP     EXEC PGM=CLIP,DPRTY=10                            * 00032
*        //STEPLIB  DD  <TO A AUTHORIZED LIBRARY, OPTIONAL>           * 00033
*        //SYSPRINT DD SYSOUT=K <OUTPUT FORM ICKDSF>                  * 00034
*        //SYSIN    DD DSN=&&CLIP,UNIT=SYSDA,SPACE=(TRK,(1)),         * 00035
*        //            DCB=(RECFM=FB,LRECL=80,BLKSIZE=80)             * 00036
*                                                                     * 00037
*        WRITTEN BY                                                   * 00038
*                                                                     * 00039
*             KENNETH RASMUSSEN                                       * 00040
*             ROCKWELL INTERNATIONAL                                  * 00041
*             P O BOX 2515  041/SH28                                  * 00042
*             SEAL BEACH, CA. 90740                                   * 00043
*             (213) 594-2540                                          * 00044
*                                                                     * 00045
*         MODIFIED BY:                                                * 00046
*                                                                     * 00047
*             TO ELIMINATE UNEEDED GETMAIN AND FREEMAIN               * 00048
*             AND FOR DOWNWARD COMPATABILITY BETWEEN                  * 00049
*             A 370 AND XA SYSTEM.                                    * 00050
*                                                                     * 00051
*             DEAN MCCOY   WARNER BROTHERS                            * 00052
*             TO USE PROGRAM PRODUCT DSF                              * 00053
*             AND OPTIONALY INSPECT A VOLUME                          * 00054
*             DEFAULTING TO OPTIONS:                                  * 00055
*              ALL TRACKS NOASSIGN NOCHECK NOVERFY                    * 00056
*                                                                     * 00057
*             9/20/85 CORRECT UCBSCAN PARMLIST FOR                    * 00058**3
*             MULTIPLE PASSES,  DELETED PRE-SP UCBSCAN CODE.          * 00059**3
*                                                                     * 00060**3
*                                                                     * 00061**3
*********************************************************************** 00062
         EJECT                                                          00063
         SPLEVEL SET=1 THIS IS FOR DOWNWARD COMPATABILITY WITH 370 ONLY 00064
CLIP     CSECT                                                          00065
         STM   R14,R12,12(R13)        SAVE OUR REGS                     00066
         USING CLIP,R12               SET UP BASE REGISTURE             00067
         LR    R12,R15                NOW LOAD IT                       00068
         ST    R13,SAVEAREA+4         LINK SAVEAREAS                    00069
         LA    R13,SAVEAREA           NOW LOAD IT                       00070
*        REGEQU                                                         00071
MAINLOOP OPEN  (SYSIN,(OUTPUT))         CREATE THE CONTROL CARD         00072
ASKAGAIN XC    CLIPECB,CLIPECB          RESET ECB WORD                  00073
         MVC   CLIPRPLY(10),BLANKS      RESET REPLY AREA                00074
         WTOR  'CLIP - REPLY UUU,VVVVVV OR UUU,INSPCT OR END',         C00075
               CLIPRPLY,10,CLIPECB,ROUTCDE=2                            00076
         WAIT  ECB=CLIPECB                                              00077
         OC    CLIPRPLY(10),BLANKS      SET REPLY TO CAPS               00078
         CLC   CLIPRPLY(3),=CL3'END'    REPLY = END ?                   00079
         BE    ENDPGM                   IF YES, THEN BRANCH             00080
         EJECT                                                          00081
*********************************************************************** 00082
*                                                                     * 00083
*        CHECK REPLY AND VALIDATE DEVICE ADDRESS                      * 00084
*                                                                     * 00085
*********************************************************************** 00086
         CLC   =C'INSPCT',CLIPRPLY+3    REPLY = INSPECT                 00087
         BE    CHKRPLY2                   THEN GO ON                    00088
         CLI   CLIPRPLY+3,C','          CHECK MSG FORMAT                00089
         BE    CHKRPLY2                 IF OK, THEN BRANCH              00090
         CLI   CLIPRPLY+4,C' '          CHECK MSG FORMAT                00091
         BNE   CHKRPLY2                 IF OK, THEN BRANCH              00092
CHKRPLY1 LA    R1,WTO01                 INVALID MSG FORMAT              00093
         SVC   35                                                       00094
         B     ASKAGAIN                                                 00095
         USING CVTMAP,R2                                                00096
CHKRPLY2 L     R2,CVTPTR                GET CVT POINTER                 00097
         OI    PARMUCB,X'80'           FLAG AS END OF PARMS             00098
*        L     R2,CVTILK2(,R2)          GET TABLE POINTER               00099
         USING UCB,R4                                                   00100
         LA    R3,CLIPRPLY              POINT AT SPECIFIED DEVICE       00101
*        SR    R4,R4                                                    00102
         MVI   WORKAREA,X'00'           CLEAR UCBSCAN WORK AREA         00103**3
         MVC   WORKAREA+1(L'WORKAREA-1),WORKAREA                        00104**3
UCBSCAN  DS    0H                                                       00105
         LA    R1,PARMLIST                                              00106
         L     R15,CVTUCBSC            GET ADDRESS OF ROUTINE           00107
         BALR  R14,R15                 CALL IT                          00108
         LTR   R15,R15                 Q. FOUND A UCB                   00109
         BNZ   UCBERROR                A. NOT ZERO - YES                00110
         L     R4,ADDRUCB              POINT TO FOUND UCB               00111
         CLC   UCBNAME(3),0(R3)         MATCH ON UCB ?                  00112
         BE    CHKDEV1                  BRANCH IF FOUND                 00113
UCBLOOP  DS    0H                                                       00114
         B     UCBSCAN                  LOOP THRU TABLE                 00115
UCBERROR MVC   WTO02+11(3),0(R3)        INSERT BAD UCB ADDRESS          00116
         LA    R1,WTO02                                                 00117
         SVC   35                                                       00118
         B     ASKAGAIN                                                 00119
CHKDEV1  CLI   UCBTBYT3,UCB3DACC        DIRECT ACCESS DEVICE ?          00120
         BE    CHKDEV2                  BRANCH IF YES                   00121
         MVC   WTO03+11(3),0(R3)        INSERT UCB ADDRESS              00122
         LA    R1,WTO03                                                 00123
         SVC   35                                                       00124
         B     ASKAGAIN                                                 00125
CHKDEV2  TM    UCBSTAT,UCBONLI          DEVICE OFFLINE ?                00126
         BO    CHKDEV3                  IF ONLINE, THEN BRANCH          00127
         TM    UCBSTAT,UCBCHGS          DEVICE CHANGING STATUS ?        00128
         BNO   DASDLABL                 IF NOT, THEN BRANCH             00129
CHKDEV3  TM    UCBSTAT,UCBPRES          DEVICE PERM RESIDENT ?          00130
         BNO   CHKDEV4                  IF NOT, THEN BRANCH             00131
         MVC   WTO04+11(3),0(R3)        INSERT UCB ADDRESS              00132
         LA    R1,WTO04                                                 00133
         SVC   35                                                       00134
         B     ASKAGAIN                                                 00135
CHKDEV4  MVC   WTO05+11(3),0(R3)        INSERT UCB ADDRESS              00136
         LA    R1,WTO05                 DEVICE NOT OFFLINE              00137
         SVC   35                                                       00138
         B     ASKAGAIN                                                 00139
         DROP  R4                                                       00140
         EJECT                                                          00141
*********************************************************************** 00142
*                                                                     * 00143
*        INVOKE DSF TO RELABEL VOLUME                                 * 00144
*                                                                     * 00145
*********************************************************************** 00146
DASDLABL CLC   =C'INSPCT',CLIPRPLY+3    REPLY = INSPECT                 00147**2
         BE    INSPECT                   IF SO THEN INSPECT             00148
         MVC   CLIPUNIT(3),0(R3)        SETUP FOR ICKDSF                00149
         MVC   CLIPVOL(6),CLIPRPLY+4     UNIT AND VOLUME                00150
         PUT   SYSIN,INPUT                                              00151
         B     CLOSEIT                                                  00152
INSPECT  MVC   CLIPUNI2(3),0(R3)                                        00153
         PUT   SYSIN,INPUT2                                             00154
CLOSEIT  CLOSE SYSIN                                                    00155
         LINK  EP=ICKDSF,PARAM=(CLIPLIST),VL=1                          00156
         C     R15,RETCODE              CHK HIGHEST RETURN CODE         00157
         BNH   DASDRET                                                  00158
         ST    R15,RETCODE              SAVE RETURN CODE                00159
DASDRET  LTR   R5,R15                   REURN CODE = 0 ?                00160
         BNZ   DASDRERR                 IF NOT, THEN BRANCH             00161
         CLC   =C'INSPCT',CLIPRPLY+3    REPLY = INSPECT                 00162**2
         BE    INSPEND                                                  00163
         MVC   WTO06+11(3),CLIPRPLY     EDIT UNIT                       00164
         MVC   WTO06+36(6),CLIPRPLY+4    AND VOLUME                     00165
         LA    R1,WTO06                                                 00166
         SVC   35                                                       00167
         B     MAINLOOP                 LOOP AGAIN                      00168
INSPEND  MVC   WTO09+11(3),CLIPRPLY     EDIT UNIT                       00169
         LA    R1,WTO09                                                 00170
         SVC   35                                                       00171
         B     MAINLOOP                 LOOP AGAIN                      00172
DASDRERR MVC   WTO06+11(3),CLIPRPLY     ERROR IN ICKDSF EXECUTION       00173
         LA    R1,WTO07                                                 00174
         SVC   35                                                       00175
         B     MAINLOOP                 LOOP AGAIN                      00176
ENDPGM   LA    R1,WTO08                                                 00177
         SVC   35                                                       00178
         L     R15,RETCODE              PICK UP RETURN CODE             00179
         L     R13,SAVEAREA+4           RESTORE CALLERS STUFF           00180
         RETURN (14,12)                 NOW RETURN                      00181
*********************************************************************** 00182
*                                                                     * 00183
*        WORKAREA AND CONSTANTS                                       * 00184
*                                                                     * 00185
*********************************************************************** 00186
         DS    0D                      ALIGN                            00187
WORKAREA DS    CL100                                                    00188
SAVEAREA DC    18F'0'                   SAVE AREA FOR ICKDSF            00189
CLIPECB  DC    F'0'                     WTOR ECB                        00190
RETCODE  DC    F'0'                     ICKDSF RETURN CODE (HIGHEST)    00191
ADDRUCB  DC    A(0)                    ADDRESS OF RETURNED UCB          00192
PARMLIST EQU   PARMWA                                                   00193
PARMWA   DC    A(WORKAREA)                                              00194
PARMDEVT DC    A(DEVCLASS)                                              00195
PARMUCB  DC    A(ADDRUCB)                                               00196
DEVCLASS DC    X'00'                   LOOK AT THEM ALL                 00197
TABLEEND DC    X'0000FFFF'              FOR UCB TABLE END CHECK         00198
CLIPLIST DS    0H                       ICKDSF PARAMETERS               00199
         DC    H'0'                                                     00200
CLIPRPLY DC    10C' '                   WTOR REPLY AREA                 00201
BLANKS   DC    10C' '                                                   00202
INPUT    DC    C' '                     SYSIN RECORD FOR ICKDSF         00203
         DC    C'REFORMAT UNIT('                                        00204
CLIPUNIT DC    CL3' '                                                   00205
         DC    C') VOLID('                                              00206
CLIPVOL  DC    CL6' '                                                   00207
         DC    C') NVFY'                                                00208
         DC    (80-(*-INPUT))C' '                                       00209
INPUT2   DC    C' INSPECT UNIT('        SYSIN RECORD FOR ICKDSF         00210
CLIPUNI2 DC    CL3' '                                                   00211
         DC    C') ALLTRK NASGN NCHK NVFY'                              00212
         DC    (80-(*-INPUT2))C' '                                      00213
*VTUCBSC EQU   X'434'                   ADDRESS OF UCB SCAN ROUTINE     00214
*VTPTR   EQU   X'10'                    ADDRESS OF CVT POINTER          00215
         EJECT                                                          00216
*********************************************************************** 00217
*                                                                     * 00218
*        WRITE TO OPERATOR'S                                          * 00219
*                                                                     * 00220
*********************************************************************** 00221
WTO01  WTO  'CLIP - INVALID REPLY',ROUTCDE=2,MF=L                       00222
WTO02  WTO  'CLIP - XXX IS AN INVALID DEVICE ADDRESS',ROUTCDE=2,MF=L    00223
WTO03  WTO  'CLIP - XXX IS NOT A DIRECT ACCESS DEVICE',ROUTCDE=2,MF=L   00224
WTO04  WTO  'CLIP - XXX IS A PERM RESIDENT VOLUME',ROUTCDE=2,MF=L       00225
WTO05  WTO  'CLIP - XXX IS NOT OFFLINE',ROUTCDE=2,MF=L                  00226
WTO06  WTO  'CLIP - XXX RELABELED TO VOL SER YYYYYY',ROUTCDE=2,MF=L     00227
WTO07  WTO  'CLIP - XXX ICKDSF ERROR - CHECK SYSPRINT',ROUTCDE=2,MF=L   00228
WTO08  WTO  'CLIP - RUN ENDED',ROUTCDE=2,MF=L                           00229
WTO09  WTO  'CLIP - XXX INSPECTED SUCCESSFULLY - CHECK SYSPRINT',      C00230
               ROUTCDE=2,MF=L                                           00231
         EJECT                                                          00232
SYSIN    DCB   DDNAME=SYSIN,BUFNO=1,DSORG=PS,MACRF=PM                   00233
         LTORG                                                          00234
         EJECT                                                          00235
TIOT     DSECT                                                          00236
         IEFTIOT1                                                       00237
         EJECT                                                          00238
UCB      DSECT                                                          00239
         IEFUCBOB                                                       00240
         PUSH  PRINT                                                    00241
         PRINT OFF                                                      00242
         CVT   DSECT=YES                                                00243
         POP   PRINT                                                    00244
         END CLIP                                                       00245
