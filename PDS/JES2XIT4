*          DATA SET JES2XIT4V9 AT LEVEL 014 AS OF 11/16/87
*          DATA SET JES2XIT4V8 AT LEVEL 011 AS OF 06/05/85              00001**6
        TITLE 'JES SETUP CARD TRANSLATION'                              00002**6
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *       00003**6
*    FIX TO CHECK FOR GENERATED SYSIN DD * TO PREVENT LOOP YARF *       00004**7
*    XRRN  THIS EXIT WAS CHANGED TO SUPPORT JES2 2.1.5.         *       00005*10
*    XKZS  THIS EXIT WAS CHANGED TO SUPPORT JES2 1.3.4.         *       00006*10
*                                            HJE2330            *       00007**6
*                                                               *       00008**6
*    PURPOSE:                                                   *       00009**6
*     TO FACILATE THE USE OF THE SETUP CARD BY TRANSLATING      *       00010**6
*     ANY "DSN=XXXX" TO  THE ACTUAL VOL=SER THAT THE DSN IS     *       00011**6
*     ON AND REPLACING THAT IN THE JES STATEMENT. IF THE DATA   *       00012**6
*     SET NAME IS NOT CATALOGED 'NOT CATLG' WILL BE PUT IN      *       00013**6
*     COLS 63-71.                                               *       00014**6
*                                                               *       00015**6
*     MODIFIED TO HANDLE SETUPS FOR CIS JOBS                    *       00016**6
*                                                               *       00017**6
*     MODIFIED TO INSERT /*ROUTE CARDS FOR CIS                  *       00018**6
*                                                               *       00019**6
*     MODIFIED TO INSERT //JOBLIB CARDS FOR CIS                 *       00020**6
*                                                               *       00021**6
*     MODIFIED TO PREVENT S0C4 ISSUING TMSSVC                   *       00022**6
*                                                               *       00023**6
*     ENTRY POINT = UEXIT4                                      *       00024**6
*                                                               *       00025**6
*     MODULE NAME = $XIT4                                       *       00026**6
*                                                               *       00027**6
*     INPUT (REGISTERED):                                       *       00028**6
*       R0    =0 JES2 COMMAND                                   *       00029**6
*             =4 JCL STATEMENT                                  *       00030**6
*       R1    POINTER TO 3WORD PERAMETER LIST                   *       00031**6
*             WORD 1 (+0) ADDRESS OF CONTROL STATEMENT          *       00032**6
*                         IMAGE BUFFER                          *       00033**6
*             WORD 2 (+4) ADDRESS OF EXIT FLAG BYTE             *       00034**6
*             WORD 3 (+8) ADDRESS OF JCTXWRK FIELD IN JCT       *       00035**6
*       R2-9  N/A                                               *       00036**6
*       R10   ADDRESS OF JCT                                    *       00037**6
*       R11   ADDRESS OF HCT                                    *       00038**6
*       R12   BASE REGISTURE                                    *       00039**6
*       R13   ADDRESS OF PCE                                    *       00040**6
*       R14   RETURN ADDRESS                                    *       00041**6
*       R15   ENTRY ADDRESS                                     *       00042**6
*                                                               *       00043**6
*     RETURN (REGISTERS):                                       *       00044**6
*       R0-14 N/A                                               *       00045**6
*       R15   RETURN CODE                                       *       00046**6
*             =0  PROCESS NEXT EXIT OR RETURN                   *       00047**6
*             =4  IGNORE NEXT EXIT                              *       00048**6
*             =8  IGNORE HASPRCCS PROCESSING                    *       00049**6
*             =12 CANCEL JOB                                    *       00050**6
*             =16 PURGE THE JOB                                 *       00051**6
*                                                               *       00052**6
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *       00053**6
         PRINT OFF                                                      00054**6
         COPY  $HASPGBL       COPY JES GLOBAL PARMS                     00055**6
         PRINT ON,NOGEN                                                 00056**6
**   CREATE THE MIT TABLE  **                                           00057**6
$XIT4   $MODULE SYSP=(NOGEN,GEN,NODATA,NOGEN,NOGEN),                   X00058*11
               $HASPEQU,                                               X00059**6
               $BUFFER,                                                X00060**6
               $RDRWORK,                                               X00061**6
               $JCT,                                                   X00062**6
               $JQE,                                                   X00063**6
               $PCE,                                                   X00064**6
               $CAT,                                                   X00065**6
               $HCT,                                                   X00066**6
               $MIT,                                                   X00067**8
               RPL                                                      00068**8
         TITLE 'JES SETUP CARD TRANSLATION'                             00069**6
         PRINT ON,GEN,NODATA                                            00070**6
UEXIT4  $ENTRY BASE=R12          EXIT ROUTINE ENTRY POINT               00071*12
        $SAVE                                                           00072**6
         LR    R12,R15                     LOAD BASE REGISTER           00073**6
         SLR   R15,R15                     CLEAR R15                    00074**6
         LR    R7,R1               SAVE ADDRESS PARAMETERS              00075**6
         LR    R9,R0               SAVE CARD TYPE INDICATOR             00076**6
         L     R4,0(R7)                                                 00077**6
         USING JCT,R10                                                  00078**8
*        MVC   #CARD,0(R4)                                              00079**6
*        WTO   MF=(E,WTOLIST)                                           00080**6
         BAL   R14,#CHKJOB                                              00081**6
         LTR   R15,R15             Q. CIS JOB                           00082**6
         BNZ   CHKSETUP            A. NOT ZERO - NO                     00083**6
         CLC   =C'//*',0(R4)       Q. COMMENT                           00084**6
         BE    #EXIT               A. EQUAL - YES                       00085**6
         CLC   =C'//SYSIN',0(R4)   Q. GENERATERED SYSIN                 00086**6
         BE    #EXIT               A. EQUAL - YES                       00087**6
         TM    JCTUSER0+1,HOLDBIT  Q. STILL NEED TO GO THERE            00088**6
         BZ    CARDSCAN            A. ZERO - YES                        00089**6
CHKSETUP DS    0H                                                       00090**6
         LTR   R9,R9               Q. JES2 CONTROL STATEMENT            00091**6
         BNZ   #EXIT               A. NOT ZERO - NO                     00092**6
         L     R2,0(R7)            LOAD RECORD TO WORK WITH             00093**6
         L     R4,0(R7)            LOAD RECORD START ADDRESS            00094**6
         CLC   SETUP,0(R4)         Q. JES2 CARD                         00095**6
         BNE   #EXIT               A. NOT EQUAL - NO                    00096**6
         LA    R3,25                                                    00097**6
BGNSRC   DS    0H                                                       00098**6
         CLC   #KEYWORD,0(R2)      Q.IS IT HERE                         00099**6
         BE    GOON                A. YES                               00100**6
         LA    R2,1(R2)            INCREMENT                            00101**6
         BCT   R3,BGNSRC              ONLY 25 TIMES                     00102**6
         B     #MOVECLS            RETURN                               00103**6
GOON     CLI   4(R2),C' '          Q. ANY DSNAME THERE                  00104**6
         BE    #MOVECLS            A. EQUAL - NO                        00105**6
         MVC   #DSNAME,BLANKS      CLEAR TO BLANKS                      00106**6
         LA    R1,5(R2)            SET START OF SCAN                    00107**6
         LA    R5,4(R2)            SAVE ADDRESS OF START OF DSN         00108**6
         LA    R3,44               SET MAX SCAN                         00109**6
DSNLOOP  DS    0H                  FIND FIRST BLANK                     00110**6
         CLI   0(R1),C' '          Q. BLANK HERE                        00111**6
         BE    FOUND               A. EQUAL - YES                       00112**6
         LA    R1,1(,R1)                                                00113**6
         BCT   R3,DSNLOOP                                               00114**6
FOUND    DS    0H                                                       00115**6
*        MVC   0(2,R2),=C'C='                                           00116**6
*        MVC   2(1,R2),JCTJCLAS    MOVE IN JOB CLASS                    00117**6
         SR    R1,R5               SET TO LENGTH                        00118**6
         BCTR  R1,0                SUBTRACT 1                           00119**6
         EX    R1,MOVEDSN          MOVE IN DSNAME FOR LOCATE            00120**6
         LOCATE #NAME              GET VOLSERS                          00121**6
         LTR   R15,R15             Q. CATALOGUED                        00122**6
         BZ    #CATLG              A. ZERO - YES                        00123**6
         MVC   62(9,R4),CATMSG     MOVE =C'NOT CATLG'                   00124**6
         B     #MOVECLS            NOW GO EXIT                          00125**6
#CATLG   DS    0H                                                       00126**6
         LH    R3,#VOLIST          GET # OF ENTRIES                     00127**6
         LA    R1,#VOLIST+6                                             00128**6
*        LA    R5,4(,R2)           POINT PAST 'C=? '                    00129**6
         LA    R5,0(,R2)           POINT TO START                       00130**6
         LA    R6,62(,R2)          POINT TO END OF CARD                 00131**6
         SR    R6,R5               GET LENGTH                           00132**6
         MVI   0(R5),C' '          CLEAR REST OF CARD                   00133**6
         EX    R6,CLEAR            CLEAR WITH LENGTH IN R6              00134**6
         LA    R6,67(,R4)          POINT TO END OF CARD - 3             00135**6
         SH    R6,SIX              MINUS SIX                            00136**6
#VOLLOOP DS    0H                                                       00137**6
         CR    R5,R6               Q. ANY MORE ROOM                     00138**6
         BH    #MOVECLS            A. HIGH - NO                         00139**6
         MVC   0(6,R5),0(R1)                                            00140**6
         CR    R6,R5               Q. ANY MORE ROOM                     00141**6
         BNH   #CHKVOLS            A. NOT HIGH - NO                     00142**6
         MVI   6(R5),C','                                               00143**6
         LA    R5,7(,R5)                                                00144**6
         LA    R1,12(R1)                                                00145**6
         BCT   R3,#VOLLOOP                                              00146**6
         BCTR  R5,0                                                     00147**6
         MVI   0(R5),C' '                                               00148**6
#CHKVOLS DS    0H                                                       00149**6
         LA    R1,JCTJNAME                                              00150**6
         BAL   R14,#CHKJOB          CHECK JOBNAME                       00151**6
         LTR   R15,R15              Q. NEED TO CHECK                    00152**6
         BNZ   #MOVECLS             A. NOT EQUAL - NO                   00153**6
         ST    R12,0(R13)           FOR UCC                             00154**6
         OPENTMC NOTACT=#EXIT                                           00155**6
         LR    R3,R2                POINT TO START OF VOLS              00156**6
#CHKLOOP DS    0H                                                       00157**6
         CLI   0(R3),C' '           Q. END OF THE TESTS                 00158**6
         BE    #MOVECLS             A. EQUAL - YES                      00159**6
         CLI   0(R3),C'0'           Q. NEED TO CHECK                    00160**6
         BNE   #NXTVOL              A. NOT EQUAL - NO                   00161**6
         MVC   TMVOLSER,0(R3)       COPY VOLSER                         00162**6
         MVI   TMVOLSER,C'C'        FORCE A 'C'                         00163**6
         GETTMC TMVOLSER,TMRECORD,#NXTVOL,ENQ=NO                        00164**6
         CLC   TMDSN,#DSNAME        Q. SAME DATASET                     00165**6
         BNE   #NXTVOL              A. NOT EQUAL - NO                   00166**6
         MVI   0(R3),C'C'           CHANGE IT TO A 'C'                  00167**6
#NXTVOL  DS    0H                                                       00168**6
         LA    R3,7(,R3)            A. SKIP OVER XXXXXX,                00169**6
         B     #CHKLOOP                                                 00170**6
#MOVECLS DS    0H                                                       00171**6
         MVC   67(2,R4),=C'C='                                          00172**6
         MVC   69(1,R4),JCTJCLAS                                        00173**6
         B     #EXIT                                                    00174**6
*CHKJCLS DS    0H                                                       00175**6
*        TM    JCTUSER0+1,CLSBIT   Q. INSERTED JOBCLASS CARD            00176**6
*        BO    #EXIT               A. ONE - YES                         00177**6
*        MVC   JCTXWRK,SETUPM                                           00178**6
*        MVC   JCTXWRK+18(1),JCTJCLAS      POST JOB CLASS               00179**6
*        OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT  00180**9
*        OI    JCTUSER0+1,CLSBIT           SET AS INSERTED              00181**6
*        LA    R15,8                                                    00182**6
*        B     #ALTEXIT                                                 00183**6
#EXIT    DS    0H                                                       00184**6
         LA    R15,0                       SET RETURN CODE              00185**6
#ALTEXIT DS    0H                                                       00186**6
        $RETURN RC=(R15)                   RETURN TO HASPPRPU           00187**6
CARDSCAN DS    0H                                                       00188**6
*        CLC   =C'$XIT4',44(R4)            Q. HAVE THESE CARDS BEEN ADD 00189*14
*        BNE   CHKROUT                     A. NO- GO ADD THEM           00190*14
*        MVI   JCTUSER0+1,X'FF'            TURN ALL BITS ON TO SAY DONE 00191*14
*        B     #EXIT                       GO EXIT                      00192*14
*HKROUT  DS    0H                                                       00193*14
         TM    JCTUSER0+1,ROUTEBIT         Q. ROUTE CARD NEEDED         00194**6
         BO    CHKPUNCH                    A. ONE - NO                  00195**6
         L     R1,0(R7)                    POINT TO JCL IMAGE           00196**6
         MVC   #HOLD,0(R1)                 COPY ORIGINAL STATEMENT      00197**6
         MVI   2(R1),C'*'                  MAKE IT A COMMENT            00198**6
         MVC   JCTXWRK,ROUTE               MOVE IN ROUTE CARD           00199**6
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT  00200**9
         OI    JCTUSER0+1,ROUTEBIT         SET AS INSERTED              00201**6
         LA    R15,8                       MAKE A COMMENT               00202**6
         CLI   JCTJNAME+2,C'T'             Q. TEST JOB                  00203**6
         BNE   #ALTEXIT                    A. NOT EQUAL - NO            00204**6
         MVC   JCTXWRK,PUNCH               MOVE IN /*ROUTE PUNCH        00205**6
         OI    JCTUSER0+1,PUNCHBIT+OUTBIT  ROUTE ONLY PUNCH             00206**6
         LA    R15,8                       MAKE A COMMENT               00207**6
         B     #ALTEXIT                    EXIT                         00208**6
CHKPUNCH DS    0H                                                       00209**6
         TM    JCTUSER0+1,PUNCHBIT         Q. ALREADY DONE              00210**6
         BO    CHKOUT                      A. ONE - YES                 00211**6
         MVC   JCTXWRK,PUNCH               MOVE IN OUTPUT CARD          00212**6
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT  00213**9
         OI    JCTUSER0+1,PUNCHBIT         SET AS INSERTED              00214**6
         B     #EXIT                       EXIT                         00215**6
CHKOUT   DS    0H                                                       00216**6
         TM    JCTUSER0+1,OUTBIT           Q. ALREADY DONE              00217**6
         BO    CHKLIB                      A. ONE - YES                 00218**6
         MVC   JCTXWRK,OUTPUT              MOVE IN OUTPUT CARD          00219**6
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT  00220**9
         OI    JCTUSER0+1,OUTBIT           SET AS INSERTED              00221**6
         B     #EXIT                       EXIT                         00222**6
CHKLIB   DS    0H                                                       00223**6
         TM    JCTUSER0+1,LIBBIT           Q. ALREADY DONE              00224**6
         BO    CHKHOLD                     A. ONE - YES                 00225**6
         MVC   JCTXWRK,JOBLIB              MOVE IN OUTPUT CARD          00226**6
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT  00227**9
         OI    JCTUSER0+1,LIBBIT           SET AS INSERTED              00228**6
         B     #EXIT                       EXIT                         00229**6
CHKHOLD  DS    0H                                                       00230**6
         MVC   JCTXWRK,#HOLD               MOVE IN HELD CARD            00231**6
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT  00232**9
         OI    JCTUSER0+1,HOLDBIT          SET AS INSERTED              00233**6
         B     #EXIT                       EXIT                         00234**6
#CHKJOB  DS    0H                                                       00235**6
         LA    R15,0                                                    00236**6
         CLI   JCTUSER0,X'FF'             Q. CIS JOB                    00237**6
         BER   R14                        A. EQUAL - YES                00238**6
         LA    R15,4                                                    00239**6
         BR    R14                                                      00240**6
* FLAGS                                                                 00241**6
JOBBIT   EQU   B'00000001'                JOB CARD FOUND                00242**6
CONTBIT  EQU   B'00000010'                LAST CARD CONTINUED           00243**6
ROUTEBIT EQU   B'00000100'                ROUTE PRINT CARD INSERTED     00244**6
OUTBIT   EQU   B'00001000'                OUTPUT CARD INSERTED          00245**6
PUNCHBIT EQU   B'00010000'                ROUTE PUNCH CARD INSERTED     00246**6
HOLDBIT  EQU   B'00100000'                CARD NEEDS TO BE RE-INSERTED  00247**6
LIBBIT   EQU   B'01000000'                JOBLIB CARD HAS BEEN INSERTED 00248**6
CLSBIT   EQU   B'10000000'                JOBCLASS CARD INSERTED        00249**6
MOVEDSN  MVC   #DSNAME(0),4(R2)            MOVE IN DSNAME               00250**6
CLEAR    MVC   1(0,R5),0(R5)                                            00251**6
#NAME    CAMLST NAME,#DSNAME,,#VOLIST                                   00252**6
SAVEJOB  DS    A                                                        00253**6
#VOLIST  DS    0H,265C                                                  00254**6
TMRECORD TMRECORD                                                       00255**6
WTOLIST  DC    AL2(L'PREFIX+L'#CARD+4,0)                                00256**6
PREFIX   DC    C'$XIT4 - '                                              00257**6
#CARD    DS    CL80                                                     00258**6
#HOLD    DS    CL80                                                     00259**6
PUNCH    DC    CL80'/*ROUTE PUNCH WBWEST                        $XIT4'  00260**6
ROUTE    DC    CL80'/*ROUTE PRINT WBEAST                        $XIT4'  00261**6
OUTPUT   DC    CL80'//JESDS OUTPUT JESDS=ALL,DEST=LOCAL,CLASS=W $XIT4'  00262**6
JOBLIB   DC    CL80'//JOBLIB DD DSN=WDS.LINKLIB,DISP=SHR        $XIT4'  00263**6
SETUPM   DC    CL80'/*SETUP JOB CLASS=                          $XIT4'  00264**6
#DSNAME  DS    CL44                                                     00265**6
CATMSG   DC    CL9'NOT CATLG'                                           00266**6
SETUP    DC    C'/*SETUP'                                               00267**6
JOB      DC    C' JOB '                                                 00268**6
#KEYWORD DC    CL4'DSN='                                                00269**6
BLANKS   DC    CL44' '                                                  00270**6
TWO      DC    H'2'                                                     00271**6
SIX      DC    H'6'                                                     00272**6
         LTORG                                                          00273**6
        $MODEND                                                         00274**6
         END    ,                                                       00275**6
